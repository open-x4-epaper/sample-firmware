#include <Arduino.h>
#include <Fonts/FreeMonoBold18pt7b.h>
#include <Fonts/FreeMonoBold12pt7b.h>
#include <GxEPD2_BW.h>
#include <SPI.h>
#include "image.h"
#include "Pangodream_18650_CL.h"

// Display SPI pins (custom pins for XteinkX4, not hardware SPI defaults)
#define EPD_SCLK 8  // SPI Clock
#define EPD_MOSI 10 // SPI MOSI (Master Out Slave In)
#define EPD_CS 21   // Chip Select
#define EPD_DC 4    // Data/Command
#define EPD_RST 5   // Reset
#define EPD_BUSY 6  // Busy

// Button pins (ADC resistor ladders)
#define BTN_GPIO1 1 // 4 buttons: Back, Confirm, Left, Right
#define BTN_GPIO2 2 // 2 buttons: Volume Up, Volume Down
#define BTN_GPIO3 3 // Power button
#define BAT_GPIO  0 // Battery voltage

#define ADC_PIN 0
#define CONV_FACTOR 1.5176
#define READS 10
#define CHARGER_THRESHOLD 2770

Pangodream_18650_CL BL(ADC_PIN, CONV_FACTOR, READS);

static int rawBat = 0;

// Button enum
enum Button
{
  NONE = 0,
  RIGHT,
  LEFT,
  CONFIRM,
  BACK,
  VOLUME_UP,
  VOLUME_DOWN,
  POWER
};

// Display command enum
enum DisplayCommand
{
  DISPLAY_NONE = 0,
  DISPLAY_HEADER,
  DISPLAY_TEXT,
  DISPLAY_SLEEP,
  TOGGLE_IMAGE
};

// Button ADC thresholds
const int BTN_THRESHOLD = 100; // Threshold tolerance
const int BTN_POWER_VAL = 3;
const int BTN_RIGHT_VAL = 3;
const int BTN_LEFT_VAL = 1470;
const int BTN_CONFIRM_VAL = 2655;
const int BTN_BACK_VAL = 3470;
const int BTN_VOLUME_DOWN_VAL = 3;
const int BTN_VOLUME_UP_VAL = 2205;

// Get button name as string
const char *getButtonName(Button btn)
{
  switch (btn)
  {
  case NONE:
    return "Press any button";
  case RIGHT:
    return "RIGHT pressed!";
  case LEFT:
    return "LEFT pressed!";
  case CONFIRM:
    return "CONFIRM pressed!";
  case BACK:
    return "BACK pressed!";
  case VOLUME_UP:
    return "VOLUME UP pressed!";
  case VOLUME_DOWN:
    return "VOLUME DOWN pressed!";
  case POWER:
    return "POWER pressed!";
  default:
    return "";
  }
}

// Get currently pressed button by reading ADC values
Button GetPressedButton()
{
  int btn1 = analogRead(BTN_GPIO1);
  int btn2 = analogRead(BTN_GPIO2);
  int btn3 = analogRead(BTN_GPIO3);

  // Check BTN_GPIO3 (Power button)
  if (btn3 < BTN_POWER_VAL + BTN_THRESHOLD)
  {
    return POWER;
  }
  // Check BTN_GPIO1 (4 buttons on resistor ladder)
  if (btn1 < BTN_RIGHT_VAL + BTN_THRESHOLD)
  {
    return RIGHT;
  }
  else if (btn1 < BTN_LEFT_VAL + BTN_THRESHOLD)
  {
    return LEFT;
  }
  else if (btn1 < BTN_CONFIRM_VAL + BTN_THRESHOLD)
  {
    return CONFIRM;
  }
  else if (btn1 < BTN_BACK_VAL + BTN_THRESHOLD)
  {
    return BACK;
  }

  // Check BTN_GPIO2 (2 buttons on resistor ladder)
  if (btn2 < BTN_VOLUME_DOWN_VAL + BTN_THRESHOLD)
  {
    return VOLUME_DOWN;
  }
  else if (btn2 < BTN_VOLUME_UP_VAL + BTN_THRESHOLD)
  {
    return VOLUME_UP;
  }

  return NONE;
}

// GxEPD2 display - Using GxEPD2_426_GDEQ0426T82
// Note: XteinkX4 has 4.26" 800x480 display
GxEPD2_BW<GxEPD2_426_GDEQ0426T82, GxEPD2_426_GDEQ0426T82::HEIGHT> display(
  GxEPD2_426_GDEQ0426T82(EPD_CS, EPD_DC, EPD_RST, EPD_BUSY));

// FreeRTOS task for non-blocking display updates
TaskHandle_t displayTaskHandle = NULL;
volatile DisplayCommand displayCommand = DISPLAY_NONE;
volatile Button currentPressedButton = NONE;
volatile bool imageVisible = false;

// Display update task running on separate core
void displayUpdateTask(void *parameter)
{
  while (1)
  {
    if (displayCommand != DISPLAY_NONE)
    {
      DisplayCommand cmd = displayCommand;
      displayCommand = DISPLAY_NONE;

      if (cmd == DISPLAY_HEADER)
      {
        // Use full window for initial welcome screen with header
        display.setFullWindow();
        display.firstPage();
        do
        {
          display.fillScreen(GxEPD_WHITE);

          // Header font
          display.setFont(&FreeMonoBold18pt7b);
          display.setCursor(20, 50);
          display.print("XteinkX4 Sample");

          // Button text with smaller font
          display.setFont(&FreeMonoBold12pt7b);
          display.setCursor(20, 100);
          display.print(getButtonName(currentPressedButton));
          display.setCursor(20, 200);
          display.print(esp_reset_reason());
        } while (display.nextPage());
      }
      else if (cmd == DISPLAY_TEXT)
      {
        // Use partial refresh for text updates
        display.setPartialWindow(0, 75, display.width(), 300);
        display.firstPage();
        do
        {
          display.fillScreen(GxEPD_WHITE);
          display.setFont(&FreeMonoBold12pt7b);
          display.setCursor(20, 100);
          display.print(getButtonName(currentPressedButton));
          display.setCursor(20, 160);
          display.printf("Battery: %s", rawBat > CHARGER_THRESHOLD ? "Charging" : "Discharging");
          display.setCursor(40, 200);
          display.printf("Raw: %i", rawBat);
          display.setCursor(40, 240);
          display.printf("Volts: %.2f", BL.getBatteryVolts());
          display.setCursor(40, 280);
          display.printf("Charge level: %i%%", BL.getBatteryChargeLevel());
        } while (display.nextPage());
      }
      else if (cmd == TOGGLE_IMAGE)
      {
        // Toggle image visibility and use partial refresh
        imageVisible = !imageVisible;
        display.setPartialWindow(20, 150, 263, 280);
        display.firstPage();
        do
        {
          if (imageVisible)
          {
            display.drawBitmap(20, 150, dr_mario, 263, 280, GxEPD_BLACK);
          }
          else
          {
            display.fillRect(20, 150, 263, 280, GxEPD_WHITE);
          }
        } while (display.nextPage());
      }
      else if (cmd == DISPLAY_SLEEP)
      {
        // Use full window for initial welcome screen with header
        display.setFullWindow();
        display.firstPage();
        do
        {
          display.fillScreen(GxEPD_WHITE);
          // Header font
          display.setFont(&FreeMonoBold18pt7b);
          display.setCursor(120, 380);
          display.print("Sleeping...");
        } while (display.nextPage());
      }
    }
    vTaskDelay(10 / portTICK_PERIOD_MS);
  }
}

void setup()
{
  // Check if boot was triggered by the Power Button (Deep Sleep Wakeup)
  // If triggered by RST pin or Battery insertion, this will be false, allowing normal boot.
  if (esp_sleep_get_wakeup_cause() == ESP_SLEEP_WAKEUP_GPIO)
  {
    // Woke up from sleep via Power Button. Verifying long press...
    // Temporarily configure pin as digital input to check state
    pinMode(BTN_GPIO3, INPUT);

    const unsigned long LONG_PRESS_MS = 1500; // 1.5 seconds required
    unsigned long pressStart = millis();
    bool bootConfirmed = true;

    // Monitor button state for the duration
    while (millis() - pressStart < LONG_PRESS_MS)
    {
      // If button reads HIGH (released) before time is up
      if (digitalRead(BTN_GPIO3) == HIGH)
      {
        bootConfirmed = false;
        break;
      }
      delay(10);
    }

    if (!bootConfirmed)
    {
      // Button released too early. Returning to sleep.
      pinMode(BTN_GPIO3, INPUT_PULLUP);
      // IMPORTANT: Re-arm the wakeup trigger before sleeping again
      esp_deep_sleep_enable_gpio_wakeup(1ULL << BTN_GPIO3, ESP_GPIO_WAKEUP_GPIO_LOW);
      esp_deep_sleep_start();
    }
  }

  Serial.begin(115200);

  // Wait for serial monitor
  unsigned long start = millis();
  while (!Serial && (millis() - start) < 3000)
  {
    delay(10);
  }


  Serial.println("\n=================================");
  Serial.println("  xteink x4 sample");
  Serial.println("=================================");
  Serial.println();

  // Initialize button pins
  pinMode(BAT_GPIO, INPUT);
  pinMode(BTN_GPIO1, INPUT);
  pinMode(BTN_GPIO2, INPUT);
  pinMode(BTN_GPIO3, INPUT);

  // Initialize SPI with custom pins
  SPI.begin(EPD_SCLK, -1, EPD_MOSI, EPD_CS);

  // Initialize display
  display.init(115200);

  // Setup display properties
  display.setRotation(3); // 270 degrees
  display.setTextColor(GxEPD_BLACK);

  Serial.println("Display initialized");

  // Draw initial welcome screen
  currentPressedButton = NONE;
  displayCommand = DISPLAY_HEADER;

  // Create display update task on core 0 (main loop runs on core 1)
  xTaskCreatePinnedToCore(displayUpdateTask,  // Task function
                          "DisplayUpdate",    // Task name
                          4096,               // Stack size
                          NULL,               // Parameters
                          1,                  // Priority
                          &displayTaskHandle, // Task handle
                          0                   // Core 0
  );

  Serial.println("Display task created");
  Serial.println("Setup complete!\n");
}

void loop()
{
  static Button lastButton = NONE;
  Button currentButton = GetPressedButton();

  // Detect button press (transition from NONE to a button)
  if (currentButton != NONE && lastButton == NONE)
  {
    Serial.print("Button: ");
    Serial.println(getButtonName(currentButton));
    currentPressedButton = currentButton;


    if (currentButton == POWER)
    {
      Serial.println("Power button pressed");
      displayCommand = DISPLAY_TEXT;
      // 1. Switch pin to Digital Input with Pull-up
      // This is crucial! 'analogRead' configures the pin as ADC, often disabling the digital input buffer.
      // If the digital buffer is disabled, the sleep controller reads '0' (LOW) and wakes immediately.
      // The Pull-up also prevents the pin from floating if the external circuit is open.
      pinMode(BTN_GPIO3, INPUT_PULLUP);

      // 2. Wait for button release
      // We wait until the digital state is HIGH (released)
      unsigned long pressedTime = millis();
      bool longPress = false;
      while (digitalRead(BTN_GPIO3) == LOW)
      {
        delay(50);
        if (millis() - pressedTime > 2000)
        {
          longPress = true;
          break;
        }
      }

      // Power button long pressed, go to sleep
      if (longPress)
      {
        displayCommand = DISPLAY_SLEEP;
        Serial.println("Power button released after a long press. Entering deep sleep.");
        delay(2000); // Allow Serial buffer to empty and display to update

        // 3. Enable Wakeup on LOW
        esp_deep_sleep_enable_gpio_wakeup(1ULL << BTN_GPIO3, ESP_GPIO_WAKEUP_GPIO_LOW);

        // 4. Enter Deep Sleep
        esp_deep_sleep_start();
      }
    }

    if (currentButton == CONFIRM)
    {
      displayCommand = TOGGLE_IMAGE;
    }
    else
    {
      displayCommand = DISPLAY_TEXT;
    }
  }

  lastButton = currentButton;

#ifdef DEBUG_IO
  // Log raw analog levels of BTN1 and BTN2 not more often than once per second
  static unsigned long lastLogMs = 0;
  unsigned long now = millis();
  if (now - lastLogMs >= 1000)
  {
    rawBat =  analogRead(BAT_GPIO);
    int rawBtn1 = analogRead(BTN_GPIO1);
    int rawBtn2 = analogRead(BTN_GPIO2);
    int rawBtn3 = analogRead(BTN_GPIO3);
    Serial.print("ADC BTN1=");
    Serial.print(rawBtn1);
    Serial.print("    BTN2=");
    Serial.print(rawBtn2);
    Serial.print("    BTN3=");
    Serial.print(rawBtn3);

    Serial.println("");

    Serial.print("Value from pin: ");
    Serial.println(rawBat);
    Serial.print("Average value from pin: ");
    Serial.println(BL.pinRead());
    Serial.print("Volts: ");
    Serial.println(BL.getBatteryVolts());
    Serial.print("Charge level: ");
    Serial.println(BL.getBatteryChargeLevel());
    Serial.println("");
    lastLogMs = now;
  }
  delay(50);
#endif
}
